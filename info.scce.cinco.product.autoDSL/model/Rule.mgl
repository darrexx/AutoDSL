import "platform:/resource/info.scce.cinco.product.autoDSL/model/SharedMemory.mgl" as mem

@primeviewer
@style("model/Rule.style")
@mcam("check")
@mcam_checkmodule("info.scce.cinco.product.autoDSL.check.ruleCheck.CheckRuleForCycles")
@mcam_checkmodule("info.scce.cinco.product.autoDSL.check.ruleCheck.CheckForUnreachableOperations")
@mcam_checkmodule("info.scce.cinco.product.autoDSL.check.ruleCheck.CheckStart")
@mcam_checkmodule("info.scce.cinco.product.autoDSL.check.ruleCheck.CheckSubRuleUpdate")
@mcam_checkmodule("info.scce.cinco.product.autoDSL.check.ruleCheck.CheckSubRuleIOIdentifiers")

graphModel Rule {
	package info.scce.cinco.product.autoDSL.rule
	nsURI "http://cinco.scce.info/product/rule"
	diagramExtension "rule"
	
	containableElements(
		Operation[1,*],
		SubRuleInputs[0,1],
		SubRuleOutputs[0,*],
		Comment[0,*],
		Save[0,*],
		Load[0,*]
	)
	
	attr EString as name
	
	
	abstract container Save extends Operation{
		incomingEdges(ControlFlow[0,*])
		outgoingEdges(ControlFlowBasic[0,1])
	}
	
	@style(commutableOperation, "Save: ${data.label}")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container SaveBoolean extends Save{
		@pvLabel(label)
		@pvFileExtension("sharedMemory")
		prime mem::StoredBoolean as data
		containableElements(BooleanInput[1,1],BooleanOutput[0,0], NumberInput[0,0], NumberOutput[0,0])
		
	}
	
	@style(commutableOperation, "Save: ${data.label}")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container SaveNumber extends Save{
		@pvLabel(label)
		@pvFileExtension("sharedMemory")
		prime mem::StoredNumber as data
		containableElements(BooleanInput[0,0],BooleanOutput[0,0], NumberInput[1,1], NumberOutput[0,0])
	}
	
	abstract container Load extends Operation{
		incomingEdges(ControlFlow[0,*])
		outgoingEdges(ControlFlowBasic[0,1])
	}
	
	@style(commutableOperation, "Load: ${data.label}")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container LoadBoolean extends Load{
		@pvLabel(label)
		@pvFileExtension("sharedMemory")
		prime mem::StoredBoolean as data
		containableElements(BooleanInput[0,0],BooleanOutput[1,1], NumberInput[0,0], NumberOutput[0,0])
	}
	
	@style(commutableOperation, "Load: ${data.label}")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container LoadNumber extends Load{
		@pvLabel(label)
		@pvFileExtension("sharedMemory")
		prime mem::StoredNumber as data 
		containableElements(BooleanInput[0,0],BooleanOutput[0,0], NumberInput[0,0], NumberOutput[1,1])
	}
	
	
	@style(commutableOperation, "${label}")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateSubRule")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.OpenSubRuleModel")
	container SubRule extends CommutableOperation {
		@pvFileExtension("rule")
		prime this::Rule as rule
		attr EString as label
		containableElements(
			BooleanCarInput[0,0], BooleanInputPort[0,0], BooleanStaticInput[0,0], NumberCarInput[0,0], NumberInputPort[0,0], NumberStaticInput[0,0],
			BooleanSubInput[0,*], NumberSubInput[0,*], BooleanSubOutput[0,*], NumberSubOutput[0,*], 
			BooleanCarOutput[0,0], BooleanOutputPort[0,0], NumberCarOutput[0,0], NumberOutputPort[0,0]
		)
	}
	
	@style(commutableOperation, "Inputs")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.AddNumberSubOutput")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.AddBooleanSubOutput")
	container SubRuleInputs extends CommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[0,0],
			BooleanSubOutput[0,*], NumberSubOutput[0,*],
			BooleanCarOutput[0,0], BooleanOutputPort[0,0], NumberCarOutput[0,0], NumberOutputPort[0,0]
		)
	}
	
	@style(commutableOperation, "Outputs")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateSubRuleOutputs")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.AddNumberSubInput")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.AddBooleanSubInput")
	container SubRuleOutputs extends CommutableOperation {
		outgoingEdges(ControlFlow[0,0])
		containableElements(
			BooleanCarInput[0,0], BooleanInputPort[0,0], BooleanStaticInput[0,0], NumberCarInput[0,0], NumberInputPort[0,0], NumberStaticInput[0,0],
			BooleanSubInput[0,*], NumberSubInput[0,*], 
			BooleanOutput[0,0], NumberOutput[0,0]
		)
	}	
	
	abstract edge ControlFlow {}
	
	@style (controlFlow)
	edge ControlFlowBasic extends ControlFlow {}
	
	@style(labeledControlFlow, "True")
	edge ControlFlowDecisionTrue extends ControlFlow {}
	
	@style(labeledControlFlow, "False")
	edge ControlFlowDecisionFalse extends ControlFlow {}
		
	@style(dataFlow)
	edge BooleanDataFlow {}
	
	@style(dataFlow)
	edge NumberDataFlow {}

	enum CarInputTypeBoolean {
		SystemOnButton
		SystemActiveButton
		HasLeadingCar
		HasEngineError
		HasSteeringError
		HasGearboxError
	}

	enum CarInputTypeNumber{
		DistanceFront
		DistanceRear
		TimeDistanceFront
		CurrentSpeed

		Steering
		SetSpeed
		LeadingCarRelativeSpeed
		Acceleration // GamepadThrottle
		SetDistance
	}

	enum CarOutputTypeBoolean {
		Scheinwerfer_An
		ObstacleDetectedWarning
		ErrorWarning
		SystemOn
		SystemActive
	}

	enum CarOutputTypeNumber {
		Acceleration
		Steering

		SetSpeed
		SetDistance
	}
	

	abstract node IO {}
	abstract node Input extends IO {}
	abstract node BooleanInput extends Input {}
	abstract node NumberInput extends Input {}
	
	abstract node Output extends IO {}
	abstract node BooleanOutput extends Output {}
	abstract node NumberOutput extends Output {}
	
	
	@style(carInput, "${inputtype}", "Boolean")
	@icon("icons/CarInput.png")
	@disable(move,resize)
	@palette("Ports")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToStatic")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToStatic")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node BooleanCarInput extends BooleanInput {
		attr CarInputTypeBoolean as inputtype
	}
	
	@style(carInput, "${inputtype}", "Number")
	@icon("icons/CarInput.png")
	@disable(move,resize)
	@palette("Ports")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToStatic")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToStatic")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node NumberCarInput extends NumberInput {
		attr CarInputTypeNumber as inputtype
	}
	
	@style(carOutput, "${outputtype}", "Boolean")
	@disable(move,resize)
	@icon("icons/CarOutput.png")
	@palette("Ports")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node BooleanCarOutput extends BooleanOutput {
		attr CarOutputTypeBoolean as outputtype
	}
	
	@style(carOutput, "${outputtype}", "Number")
	@disable(move,resize)
	@icon("icons/CarOutput.png")
	@palette("Ports")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node NumberCarOutput extends NumberOutput {
		attr CarOutputTypeNumber as outputtype
	}
		
	@style(inputPort, "Number")
	@disable(move,resize)
	@palette("Ports")
	@icon("icons/inputPort.png")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToStatic")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node NumberInputPort extends NumberInput {
		incomingEdges(NumberDataFlow[1,1])
	}

	@style(inputPort, "Boolean")
	@disable(move,resize)
	@palette("Ports")
	@icon("icons/inputPort.png")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToStatic")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node BooleanInputPort extends BooleanInput {
		incomingEdges(BooleanDataFlow[1,1])
	}
	
	@style(outputPort, "Boolean")
	@disable(move,resize)
	@icon("icons/outputPort.png")
	@palette("Ports")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node BooleanOutputPort extends BooleanOutput {
		outgoingEdges(BooleanDataFlow[1,*])
	}

	@style(outputPort, "Number")
	@disable(move,resize)
	@icon("icons/outputPort.png")
	@palette("Ports")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node NumberOutputPort extends NumberOutput {
		outgoingEdges(NumberDataFlow[1,*])
	}
	
	@style(staticInput, "${staticValue}", "Boolean")
	@disable(move,resize)
	@palette("Ports")
	@icon("icons/StaticInput.png")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node BooleanStaticInput extends BooleanInput {
		attr EBoolean as staticValue
	}
	
	@style(staticInput, "${staticValue}", "Number")
	@disable(move,resize)
	@palette("Ports")
	@icon("icons/StaticInput.png")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToCar")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@doubleClickAction("info.scce.cinco.product.autoDSL.hooks.ToPort")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node NumberStaticInput extends NumberInput {
		attr EFloat as staticValue
	}
	
	@style(subInput, "${identifier}", "Number")
	@disable(move,resize)
	@palette("Ports")
	@icon("icons/subRule_In.png")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node NumberSubInput extends NumberInput {
		attr EString as identifier
		incomingEdges(NumberDataFlow[1,1])
	}
	
	@style(subInput, "${identifier}", "Boolean")
	@disable(move,resize)
	@palette("Ports")
	@icon("icons/subRule_In.png")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node BooleanSubInput extends BooleanInput {
		attr EString as identifier
		incomingEdges(BooleanDataFlow[1,1])
	}
	
	@style(subOutput, "${identifier}", "Number")
	@disable(move,resize)
	@palette("Ports")
	@icon("icons/subRule_out.png")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node NumberSubOutput extends NumberOutput {
		attr EString as identifier
		outgoingEdges(NumberDataFlow[0,*])
	}
	
	@style(subOutput, "${identifier}", "Boolean")
	@disable(move,resize)
	@palette("Ports")
	@icon("icons/subRule_out.png")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateIO")
	@preDelete("info.scce.cinco.product.autoDSL.hooks.DeleteIO")
	node BooleanSubOutput extends BooleanOutput {
		attr EString as identifier
		outgoingEdges(BooleanDataFlow[0,*])
	}
	
	abstract container Operation {
		incomingEdges(ControlFlow[0,*])
		outgoingEdges(ControlFlowBasic[0,1])
		containableElements(Input[0,*], Output[0,*]) 
	}
   
	abstract container CommutableOperation extends Operation {}
	abstract container NonCommutableOperation extends Operation {} 

	@style(commutableOperation, "+")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Addition extends CommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[1,*],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[1,1]
		)
	}
	
	@style(commutableOperation, "×")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Multiplication extends CommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[1,*],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[1,1]
		)		
	}

	@style(nonCommutableOperation, "-")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Subtraction extends NonCommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[2,2],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[1,1]
		)
	}
	
	@style(nonCommutableOperation, "÷")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Division extends NonCommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[2,2],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[1,1]
		)
	}
	
	@style(nonCommutableOperation, "<")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Less extends NonCommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[2,2],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[1,1], NumberOutput[0,0]
		)
	}
	
	@style(nonCommutableOperation, "≤")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container LessOrEqual extends NonCommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[2,2],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[1,1], NumberOutput[0,0]
		)
	}
	
	@style(nonCommutableOperation, ">")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Greater extends NonCommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[2,2],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[1,1], NumberOutput[0,0]
		)
	}
	
	@style(nonCommutableOperation, "≥")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container GreaterOrEqual extends NonCommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[2,2],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[1,1], NumberOutput[0,0]
		)
	}
	
	@style(commutableOperation, "max")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Maximum extends CommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[1,*],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[1,1]
		)
	}
	
	@style(commutableOperation, "min")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Minimum extends CommutableOperation {
		containableElements(
			BooleanInput[0,0], NumberInput[1,*],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[1,1]
		)
	}
	
	@style(commutableOperation, "∧")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container LogicalAnd extends CommutableOperation {
		containableElements(
			BooleanInput[1,*], NumberInput[0,0],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[1,1], NumberOutput[0,0]
		)
	}
	
	@style(commutableOperation, "∨")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container LogicalOr extends CommutableOperation {
		containableElements(
			BooleanInput[1,*], NumberInput[0,0],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[1,1], NumberOutput[0,0]
		)
	}
	
	@style(commutableOperation, "¬")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Negation extends Operation {
		containableElements(
			BooleanInput[1,1], NumberInput[0,0],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[1,1], NumberOutput[0,0]
		)
	}
	
	@style(commutableOperation, "?")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container Decision extends Operation {
		outgoingEdges(ControlFlowDecisionFalse[1,1], ControlFlowDecisionTrue[1,1], ControlFlowBasic[0,0])
		containableElements(
			BooleanInput[1,1], NumberInput[0,0],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[0,0]
		)
	}
	
	@style(comment, "${comment}")
	node Comment {
		@multiLine
		attr EString as comment := ""
	}

	@style(commutableOperation, "Output")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container DirectBooleanOutput extends Operation {
		containableElements(
			BooleanInput[1,1], NumberInput[0,0],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanCarOutput[1,1], BooleanOutputPort[0,0], NumberOutput[0,0]
		)
	}
	
	@style(commutableOperation, "Output")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container DirectNumberOutput extends Operation {
		containableElements(
			BooleanInput[0,0], NumberInput[1,1],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberCarOutput[1,1], NumberOutputPort[0,0]
		)
	}
	
	@style(commutableOperation, "Guard")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container BooleanGuardOutput extends Operation {
		outgoingEdges(*[0,0])
		containableElements(
			BooleanInput[1,1], NumberInput[0,0],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[0,0]
		)
	}
	
	@style(pid, "${p}", "${i}","${d}")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container PIDController extends Operation {
		attr EDouble as p
		attr EDouble as i
		attr EDouble as d
		containableElements(
			BooleanInput[0,0], NumberInput[2,2],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[1,1]
		)
	}
	
	@style(storedPID, "${data.p}", "${data.i}","${data.d}", "${data.label}")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container StoredPIDController extends Operation{
		@pvLabel(label)
		@pvFileExtension("sharedMemory")
		prime mem::StoredPIDController as data
		containableElements(
			BooleanInput[0,0], NumberInput[2,2],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0],
			BooleanOutput[0,0], NumberOutput[1,1]
		)
	}
	
	@style(commutableOperation, "${name}")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOperation")
	@postResize("info.scce.cinco.product.autoDSL.hooks.ResizeOperation")
	container StaticNumberValue extends Operation {
		attr EString as name := ""
		containableElements(
			BooleanInput[0,0], NumberCarInput[0,0], NumberInputPort[0,0], NumberStaticInput[1,1],
			BooleanSubInput[0,0], NumberSubInput[0,0], BooleanSubOutput[0,0], NumberSubOutput[0,0], 
			BooleanOutput[0,0], NumberCarOutput[0,0], NumberOutputPort[1,1]
		)
	}
}