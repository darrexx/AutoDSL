@primeviewer
@style("model/TmpRule.style")
graphModel TmpRule {
	package info.scce.cinco.product.autoDSL.tmprule
	nsURI "http://cinco.scce.info/product/tmprule"
	diagramExtension "tmprule"
	
	containableElements(
		Operation[0,*],
		Decision[0,*],
		Comment[0,*],
		OutputNode[0,*],
		BooleanGuardOutput[0,*]
	)
	
	@style(controlFlow)
	edge ControlFlow {
		
	}
	
	@style(dataFlow)
	edge DataFlow {
		
	}
	
	enum PrimitiveType {
		Text
		Number
		Boolean
		Timestamp
	}
	
	enum CarInputType {
		a
		b
		c
	}
	
	enum CarOutputType {
		a
		b
		c
	}

	abstract node Input {
		attr PrimitiveType as datatype
	}
	abstract node Output {
		attr PrimitiveType as datatype
	}
	
	
	@style(carInput, "${inputtype}", "${datatype}")
	@disable(resize)
	@palette("Ports")
//	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.CarInputToStaticInput")  //TODO
//	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.CarInputToInputPort") //TODO
//	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.CreateCarInput") //TODO
	node CarInput extends Input {
		attr CarInputType as inputtype
	}
	
	@style(carOutput, "${outputtype}", "${datatype}")
	@disable(resize)
	@palette("Ports")
//	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.CreateCarOutput") //TODO
	node CarOutput extends Output {
		attr CarOutputType as outputtype
	}
		
	@style(inputPort, "${datatype}")
	@disable(resize)
	@palette("Ports")
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.InputPortToStaticInput") 
	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.InputPortToCarInput")
//	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.CreateInputPort") //TODO
	node InputPort extends Input {
		incomingEdges(DataFlow[1,1])
	}
	
	@style(outputPort, "${datatype}")
	@disable(resize)
	@palette("Ports")
//	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.CreateOutputPort") //TODO
	node OutputPort extends Output {
		outgoingEdges(DataFlow[1,*])
	}
	
	@style(staticInput, "${staticValue}", "${datatype}")
	@disable(resize)
	@palette("Ports")
// TODO Static InputToX
// Wie viele StaticInput-Typen gibt es und brauchen wir einen Hook für jeden? 
//	@contextMenuAction("info.scce.cinco.product.autoDSL.hooks.NumberStaticInput") //TODO
	node NumberStaticInput extends Input {
		attr EInt as staticValue
	}
	
	abstract container Operation {
		incomingEdges(ControlFlow[1,*])
		outgoingEdges(ControlFlow[1,1])
	}
	
	abstract container CommutableOperation extends Operation {
		containableElements(Input[0,*], Output[1,1])
	}
	
	abstract container NonCommutableOperation extends Operation {
		containableElements(Input[2,2], Output[1,1])
	}

	@style(commutableOperation, "+")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateCommutableNumberOp")
	container Addition extends CommutableOperation {
		
	}
	
	@style(commutableOperation, "×")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateCommutableNumberOp")
	container Multiplication extends CommutableOperation {
		
	}

	@style(nonCommutableOperation, "-")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateSubtraction")
	container Subtraction extends NonCommutableOperation {
		
	}
	
	@style(nonCommutableOperation, "<")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateComparison")
	container Less extends NonCommutableOperation {
	}
	
	@style(nonCommutableOperation, "≤")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateComparison")
	container LessOrEqual extends NonCommutableOperation {
	}
	
	@style(commutableOperation, "max")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateCommutableNumberOp")
	container Maximum extends CommutableOperation {
	}
	
	@style(commutableOperation, "min")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateCommutableNumberOp")
	container Minimum extends CommutableOperation {
	}
	
	@style(commutableOperation, "∧")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateCommutableBooleanOp")
	container LogicalAnd extends CommutableOperation {
	}
	
	@style(commutableOperation, "∨")
	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateCommutableBooleanOp")
	container LogicalOr extends CommutableOperation {
	}
	
	@style(commutableOperation, "¬")
//	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateUnaryOp")
	container Negation extends Operation {
		containableElements(Input[1,1], Output[1,1])
	}
	
	@style(decision, "?")
//	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateDecision") //TODO
	container Decision {
		incomingEdges(ControlFlow[1,*])
		outgoingEdges(ControlFlow[2,2])
		containableElements(Input[1,1])
	}
	
	@style(comment, "${comment}")
	node Comment {
		@multiLine
		attr EString as comment := ""
	}
	
	@style(commutableOperation, "Output")
//	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateOutputNode") //TODO
	container OutputNode {
		incomingEdges(ControlFlow[1,*])
		outgoingEdges(ControlFlow[0,1])
		containableElements(Input[1,1], CarOutput[1,1])
	}
	
	@style(decision, "Guard")
//	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreateBooleanGuardOutput") //TODO
	container BooleanGuardOutput {
		incomingEdges(ControlFlow[1,*])
		containableElements(Input[1,1])
	}
	
	@style(pid, "${p}", "${i}","${d}")
//	@postCreate("info.scce.cinco.product.autoDSL.hooks.CreatePIDController") //TODO
	container PIDController extends Operation{
		incomingEdges(ControlFlow[1,*])
		outgoingEdges(ControlFlow[0,1])
		attr EDouble as p
		attr EDouble as i
		attr EDouble as d
		containableElements(Input[2,2], Output[1,1])
	}
}